plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id 'org.beryx.jlink' version '2.24.1'
    id 'io.freefair.lombok' version '6.4.0'

    // Note: the java modularity plugin is added by default with javafx, this manual addition is to override the version
    // more info here https://discuss.gradle.org/t/unable-to-build-project-with-gradle-7-3-1-on-java-17/41601
    // id 'org.javamodularity.moduleplugin' version '1.8.10'
    // Removed 16 Feb - Seems to have had no effect
}

version '1.0'

jar{
    manifest{
        attributes 'Main-Class': 'app.App'
    }
}

repositories {
    mavenCentral()

    flatDir {
        dirs 'libs'
    }
}

ext {
    junitVersion = '5.8.2'
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainClass = 'app.App'
}

javafx {
    version = '17'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'org.projectlombok:lombok:1.18.24'
    implementation 'org.jgrapht:jgrapht-core:1.5.1'
    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")

    // https://mvnrepository.com/artifact/com.deepnetts/deepnetts-core
    implementation(group: 'com.deepnetts', name: 'deepnetts-core', version: '1.13.0') {
        // Excludes transitive dependency
        exclude group: 'org.apache.logging.log4j'
    }
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'app'
    }
}

jlinkZip {
    group = 'distribution'
}
